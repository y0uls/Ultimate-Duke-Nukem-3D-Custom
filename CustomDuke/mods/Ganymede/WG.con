// Below is example code that ADDS enemies/items in Duke Nukem 3D.

// "MYENEMY" is simply a three-framed object bouncing around in random angles
// mindlessly shooting the freeze weapon towards at closest player.


define MYENEMY 5296           // A position in .art file
define MYENEMY_NORMAL_STRENGTH   100
define MYENEMY_TOUGHER_STRENGTH  200
define MYENEMY_DAMAGE_TO_PLAYER  -20

definesound MYENEMY_ROAM   my_eroam.voc   0 0 0 0 0
definesound MYENEMY_HURT   my_ehurt.voc   0 0 0 0 0
definesound MYENEMY_DEAD   my_edead.voc   0 0 0 0 0
definesound MYENEMY_SHOOT  my_eshot.voc   0 0 0 0 0

action MYENEMY_ANIMATIONS  0  3  1  1  4
action MYENEMY_FRAMES   0   1   7   1   1
action MYENEMY_SCREAM   0   1   7   1   1
move MYENEMY_RUNVELS 128 64
move MYENEMY_RUNUPVELS 128 -64
move MYENEMY_BULLVELS 252 -64
move MYENEMY_BACKWARDS -64 -64
move MYENEMY_RISE 32 -32
move MYENEMY_STOPPED -16

move MYENEMY_SPEEDS 64

ai AIMYENEMY_BOUNCE   MYENEMY_ANIMATIONS MYENEMY_SPEEDS geth randomangle
ai AIMYENEMY_GETE MYENEMY_SCREAM MYENEMY_RUNVELS faceplayerslow getv
ai AIMYENEMY_WAIT MYENEMY_FRAMES MYENEMY_STOPPED faceplayerslow
ai AIMYENEMY_GETUP MYENEMY_SCREAM MYENEMY_RUNUPVELS faceplayer getv
ai AIMYENEMY_PULLBACK MYENEMY_FRAMES MYENEMY_BACKWARDS faceplayerslow
ai AIMYENEMY_HIT MYENEMY_SCREAM MYENEMY_BACKWARDS faceplayer
ai AIMYENEMY_DODGE MYENEMY_FRAMES MYENEMY_BULLVELS dodgebullet geth
ai AIMYENEMY_DODGEUP MYENEMY_FRAMES MYENEMY_RISE getv geth

useractor enemy MYENEMY

  ifai NO           // 'NO' is defined as '0'
  {               // Prepare(Initialize) MyEnemy for battle...

    // Give 'MYENEMY' strength.  If it
    // has a palette lookup other than
    // 0, make it twice as strong!

    ifspritepal 0
      strength MYENEMY_NORMAL_STRENGTH
    ifspritepal 1
      strength MYENEMY_TOUGHER_STRENGTH

ifspritepal 0
    sizeat 48 48      // Set the size of your actor
ifspritepal 1
    sizeat 96 96 
    cstator 257         // Force actor to block

    ai AIMYENEMY_BOUNCE   // Make it go!

  }

  ifrnd 8
  {

    // Used with keyword 'shoot'.
    // BLOODSPLAT1 ,BLOODSPLAT2 ,BLOODSPLAT3 ,BLOODSPLAT4 ,KNEE ,
    // SHOTSPARK1 ,SHOTGUN ,CHAINGUN ,FIRELASER ,SPIT ,COOLEXPLOSION1 ,
    // FREEZEBLAST ,RPG ,HANDHOLDINGLASER ,BOUNCEMINE ,MORTER ,SHRINKER
    // GROWSPARK

ifspritepal 0
    shoot FREEZEBLAST
ifspritepal 1
    shoot RPG


    // A sound effect to play for the shooting action.

    sound MYENEMY_SHOOT
  }

  ifcount 48   // if after about a second and a half and...
    ifrnd 16   // if a random value between 0-255 is less than 16...
      ai AIMYENEMY_BOUNCE // reset everything, including all counters

  ifpdistl 1024
    ifrnd 16
  {
    sound DUKE_GRUNT
    palfrom 24 24
    addphealth MYENEMY_DAMAGE_TO_PLAYER
  }

  ifhitweapon         // Was it hit by a weapon?
  {
    debris SCRAP1 2       // OUCH!  Make some peices fall off

    ifdead          // Is it dead yet?
    {             // If so, then...
      addkills 1        //   Add 1 kill to player score
      spawn EXPLOSION2      //   Make an explosion
      sound MYENEMY_DEAD    //   The sound of death

                    // Damage anything within a close distance.
      hitradius 2048 WEAKEST WEAK MEDIUMSTRENGTH TOUGH
      killit          //   Delete sprite (CODE ABORTS HERE)
    }

    sound MYENEMY_HURT    // No?, Make a hurt sound instead
  }
enda


useractor notenemy SUSHIPLATE6 WEAK
	ifhitweapon
	 {
	  lotsofglass 7
	  sound GLASS_BREAKING 
	  killit
       }
enda
